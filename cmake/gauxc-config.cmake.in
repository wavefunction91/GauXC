cmake_minimum_required(VERSION 3.17 FATAL_ERROR) # Require CMake 3.17+

get_filename_component(GauXC_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

list(INSERT CMAKE_MODULE_PATH 0 ${GauXC_CMAKE_DIR}                      )
list(INSERT CMAKE_MODULE_PATH 0 ${GauXC_CMAKE_DIR}/linalg-cmake-modules )
include(CMakeFindDependencyMacro)

# Always Required Dependencies
find_dependency( IntegratorXX )
find_dependency( ExchCXX      )
find_dependency( OpenMP       )


set( GAUXC_ENABLE_HOST       @GAUXC_ENABLE_HOST@      )
set( GAUXC_ENABLE_CUDA       @GAUXC_ENABLE_CUDA@      )
set( GAUXC_ENABLE_MAGMA      @GAUXC_ENABLE_MAGMA@     )
set( GAUXC_ENABLE_MPI        @GAUXC_ENABLE_MPI@       )
set( GAUXC_ENABLE_GAU2GRID   @GAUXC_ENABLE_GAU2GRID@  )


if( GAUXC_ENABLE_HOST )
  find_dependency( LAPACK )
  if( GAUXC_ENABLE_GAU2GRID )
    find_dependency( gau2grid )
  endif()
endif()

if( GAUXC_ENABLE_CUDA )
  enable_language( CUDA )
  find_dependency( CUDAToolkit @CUDAToolkit_VERSION@ EXACT )
  if( GAUXC_ENABLE_MAGMA )
    find_dependency( MAGMA )
  endif()
endif()

if( GAUXC_ENABLE_MPI )
  find_dependency( MPI )
endif()

if(NOT TARGET gauxc::gauxc)
    include("${GauXC_CMAKE_DIR}/gauxc-targets.cmake")
endif()

set(GauXC_LIBRARIES gauxc::gauxc)

list(REMOVE_AT CMAKE_MODULE_PATH 0)
list(REMOVE_AT CMAKE_MODULE_PATH 0)